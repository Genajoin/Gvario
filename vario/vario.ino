#include <Wire.h>                      //i2c library
#include "filter.h"

#define DEBUG_PRINT 1
#define UART_SPEED 115200                //define serial transmision speed (9600,19200, etc...)
#define BMP085_ADDRESS 0x77  // I2C address of BMP085


const unsigned char OSS = 1;  // Oversampling Setting https://www.sparkfun.com/tutorial/Barometric/OSS-values.PNG

// Calibration values
int ac1;
int ac2; 
int ac3; 
unsigned int ac4;
unsigned int ac5;
unsigned int ac6;
int b1; 
int b2;
int mb;
int mc;
int md;

// b5 is calculated in bmp085GetTemperature(...), this variable is also used in bmp085GetPressure(...)
// so ...Temperature(...) must be called before ...Pressure(...).
long b5; 

int Temperature;
long pressure;
long Altitude;
long average_altitude;
unsigned long tempo, tempo0;

void bmp085Calibration();
short bmp085GetTemperature(unsigned int ut);
long bmp085GetPressure(unsigned long up);
unsigned int bmp085ReadUT();
unsigned long bmp085ReadUP();
int bmp085ReadInt(unsigned char address);



Filter F;



  //************************************************************************************************************

void setup()                // setup() function to setup all necessary parameters before we go to endless loop() function
{
#if DEBUG_PRINT
  Serial.begin(UART_SPEED);       // set up arduino serial port
#endif
  Wire.begin();             // lets init i2c protocol
  bmp085Calibration();
  Temperature = int(bmp085GetTemperature(bmp085ReadUT()));
  average_altitude = get_altitude();
}

  //********************************************** LOOP **************************************************************
void loop(void)
{
  tempo= millis();
  if (tempo - tempo0 > 1000) {
    Temperature = int(bmp085GetTemperature(bmp085ReadUT()));
    tempo0=tempo;
  }
  pressure = bmp085GetPressure(bmp085ReadUP());
  // altitude in cm
  Altitude = get_altitude();
//  vario = (long)((Altitude- Altitude0)*1000/(tempo - tempo0));
//  Altitude0 = Altitude;
//  tempo0 = tempo;
  
  average_altitude = F.step(1);
//  average_vario = Filter(vario);
  

#if DEBUG_PRINT
    Serial.print(pressure);
    Serial.print(";");
    Serial.print(Altitude);
    Serial.print(";");
    Serial.print(average_altitude);
    Serial.print(";");
//Serial.print(Temperature,DEC);      
  //  Serial.print(";");
    Serial.print(tempo);
//    Serial.print(";");
//    Serial.print(";");
    Serial.println(";");
#endif
  
// ********************************************************  END LOOP  ****************************************************
}


long get_altitude()
{
  return 4433000 * (1 - pow(((float) pressure/101325), 0.190295));
}


// Stores all of the bmp085's calibration values into global variables
// Calibration values are required to calculate temp and pressure
// This function should be called at the beginning of the program
void bmp085Calibration()
{
  ac1 = bmp085ReadInt(0xAA);
  ac2 = bmp085ReadInt(0xAC);
  ac3 = bmp085ReadInt(0xAE);
  ac4 = bmp085ReadInt(0xB0);
  ac5 = bmp085ReadInt(0xB2);
  ac6 = bmp085ReadInt(0xB4);
  b1 = bmp085ReadInt(0xB6);
  b2 = bmp085ReadInt(0xB8);
  mb = bmp085ReadInt(0xBA);
  mc = bmp085ReadInt(0xBC);
  md = bmp085ReadInt(0xBE);
}

// Calculate temperature given ut.
// Value returned will be in units of 0.1 deg C
short bmp085GetTemperature(unsigned int ut)
{
  long x1, x2;
  
  x1 = (((long)ut - (long)ac6)*(long)ac5) >> 15;
  x2 = ((long)mc << 11)/(x1 + md);
  b5 = x1 + x2;

  return ((b5 + 8)>>4);  
}

// Calculate pressure given up
// calibration values must be known
// b5 is also required so bmp085GetTemperature(...) must be called first.
// Value returned will be pressure in units of Pa.
long bmp085GetPressure(unsigned long up)
{
  long x1, x2, x3, b3, b6, p;
  unsigned long b4, b7;
  
  b6 = b5 - 4000;
  // Calculate B3
  x1 = (b2 * (b6 * b6)>>12)>>11;
  x2 = (ac2 * b6)>>11;
  x3 = x1 + x2;
  b3 = (((((long)ac1)*4 + x3)<<OSS) + 2)>>2;
  
  // Calculate B4
  x1 = (ac3 * b6)>>13;
  x2 = (b1 * ((b6 * b6)>>12))>>16;
  x3 = ((x1 + x2) + 2)>>2;
  b4 = (ac4 * (unsigned long)(x3 + 32768))>>15;
  
  b7 = ((unsigned long)(up - b3) * (50000>>OSS));
  if (b7 < 0x80000000)
    p = (b7<<1)/b4;
  else
    p = (b7/b4)<<1;
    
  x1 = (p>>8) * (p>>8);
  x1 = (x1 * 3038)>>16;
  x2 = (-7357 * p)>>16;
  p += (x1 + x2 + 3791)>>4;
  
  return p;
}

// Read 2 bytes from the BMP085
// First byte will be from 'address'
// Second byte will be from 'address'+1
int bmp085ReadInt(unsigned char address)
{
  unsigned char msb, lsb;
  
  Wire.beginTransmission(BMP085_ADDRESS);
  Wire.write(address);
  Wire.endTransmission();
  
  Wire.requestFrom(BMP085_ADDRESS, 2);
  while(Wire.available()<2)    ;
  msb = Wire.read();
  lsb = Wire.read();
  
  return (int) msb<<8 | lsb;
}

// Read the uncompensated temperature value
unsigned int bmp085ReadUT()
{
  unsigned int ut;
  
  // Write 0x2E into Register 0xF4
  // This requests a temperature reading
  Wire.beginTransmission(BMP085_ADDRESS);
  Wire.write(0xF4);
  Wire.write(0x2E);
  Wire.endTransmission();
  
  // Wait at least 4.5ms
  delay(5);
  
  // Read two bytes from registers 0xF6 and 0xF7
  ut = bmp085ReadInt(0xF6);
  return ut;
}

// Read the uncompensated pressure value
unsigned long bmp085ReadUP()
{
  unsigned char msb, lsb, xlsb;
  unsigned long up = 0;
  
  // Write 0x34+(OSS<<6) into register 0xF4
  // Request a pressure reading w/ oversampling setting
  Wire.beginTransmission(BMP085_ADDRESS);
  Wire.write(0xF4);
  Wire.write(0x34 + (OSS<<6));
  Wire.endTransmission();
  
  // Wait for conversion, delay time dependent on OSS
  delay(2 + (3<<OSS));
  
  // Read register 0xF6 (MSB), 0xF7 (LSB), and 0xF8 (XLSB)
  Wire.beginTransmission(BMP085_ADDRESS);
  Wire.write(0xF6);
  Wire.endTransmission();
  Wire.requestFrom(BMP085_ADDRESS, 3);
  
  // Wait for data to become available
  while(Wire.available() < 3)
    ;
  msb = Wire.read();
  lsb = Wire.read();
  xlsb = Wire.read();
  
  up = (((unsigned long) msb << 16) | ((unsigned long) lsb << 8) | (unsigned long) xlsb) >> (8-OSS);
  
  return up;
}



